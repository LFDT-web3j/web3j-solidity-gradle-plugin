plugins {
    id 'idea'
    id 'maven-publish'
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '1.3.1'
    id 'com.diffplug.spotless' version '7.2.1'
    id 'de.undercouch.download' version '5.6.0'
}

apply {
    [
        'jacoco',
        'java',
        'javadoc',
        'repositories',
        'spotless',
        'junit'
    ].each { buildScript ->
        download.run {
            src "https://raw.githubusercontent.com/LFDT-web3j/web3j-build-tools/main/gradle/$buildScript/build.gradle"
            dest "$rootDir/gradle/$buildScript/build.gradle"
            overwrite false
            quiet true
            onlyIfModified true
        }
        from("$rootDir/gradle/$buildScript/build.gradle")
    }
}

description = 'Solidity Gradle Plugin'

gradlePlugin {
    website = 'https://web3j.io/'
    vcsUrl = 'https://github.com/LFDT-web3j/web3j-solidity-gradle-plugin'
    plugins {
        register('solidity') {
            id = 'org.web3j.solidity'
            implementationClass = 'org.web3j.solidity.gradle.plugin.SolidityPlugin'
            displayName = 'Solidity Plugin'
            description = 'Gradle plugin providing tasks to compile Solidity contracts.'
            tags = ['solidity', 'ethereum']
        }
    }
}


repositories {
    maven {
        url = 'https://plugins.gradle.org/m2/'
    }
}

ext {
    soktVersion = '0.6.0'
    kotlinxSerializationVersion = '1.6.3' // required to CompileStatic with sokt involved
    junitVersion = '5.9.3'
    assertjVersion = '3.25.3'
    systemRulesVersion = '1.19.0'
    nodePluginVersion = '7.0.2'
}

dependencies {
    implementation "org.web3j:web3j-sokt:$soktVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-core-jvm:$kotlinxSerializationVersion"
    implementation "com.github.node-gradle:gradle-node-plugin:$nodePluginVersion"
    testImplementation "org.assertj:assertj-core:$assertjVersion"
    testImplementation "com.github.stefanbirkner:system-rules:$systemRulesVersion"
}

spotless {
    groovy {
        excludeJava() // excludes all Java sources within the Groovy source dirs from formatting
        licenseHeaderFile "$rootDir/gradle/spotless/java.license"
    }
}

// Test against older supported Gradle versions.
// Test against latest 8.x release (8.5 is absolute minimum due to Java 21 requirement)
['8.14.3'].forEach { gradleVersionUnderTest ->
    def testGradle = tasks.register("testGradle$gradleVersionUnderTest", Test) {
        group = "verification"
        description = "Runs tests against Gradle $gradleVersionUnderTest"
        testClassesDirs = sourceSets.test.output.classesDirs
        classpath = sourceSets.test.runtimeClasspath
        useJUnitPlatform()
        systemProperty("gradleVersionUnderTest", gradleVersionUnderTest)
    }
    tasks.named('check') {
        dependsOn(testGradle)
    }
}
