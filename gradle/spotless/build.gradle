
apply plugin: 'com.diffplug.spotless'
apply plugin: 'de.undercouch.download'

tasks.register('downloadJavaLicense', Download) {
    src 'https://raw.githubusercontent.com/LFDT-web3j/web3j-build-tools/main/gradle/spotless/java.license'
    dest new File("$rootDir/gradle/spotless",'java.license')
    quiet true
    //Download only if the file does not already exist
    onlyIf {
        !dest.exists()
    }
    onlyIfModified true
}

tasks.register('downloadFormatterProperties', Download) {
    src 'https://raw.githubusercontent.com/LFDT-web3j/web3j-build-tools/main/gradle/spotless/formatter.properties'
    dest new File("$rootDir/gradle/spotless",'formatter.properties')
    quiet true
    onlyIf {
        !dest.exists()
    }
    onlyIfModified true
}

spotless {
    java {
        // This path needs to be relative to each project
        target fileTree('.') {
            include '**/src/*/java/**/*.java'
            exclude '**/.gradle/**'
            exclude '**/generated/**'
            exclude '**/build/**'
        }
        removeUnusedImports()
        googleJavaFormat("1.17.0").aosp()
        importOrder 'java', '', 'org.web3j', '\\#'
        trimTrailingWhitespace()
        endWithNewline()
        licenseHeaderFile "$rootDir/gradle/spotless/java.license"
    }
    kotlin {
        // This path needs to be relative to each project
        target fileTree('.') {
            include '**/*.kt'
            exclude '**/.gradle/**'
            exclude '**/build/**'
        }
        ktlint('0.49.1')
        trimTrailingWhitespace()
        endWithNewline()
        licenseHeaderFile "$rootDir/gradle/spotless/java.license"
    }
    groovyGradle {
        target '*.gradle'
        greclipse().configFile("$rootDir/gradle/spotless/formatter.properties")
        endWithNewline()
        leadingTabsToSpaces(4)
    }
}

tasks.spotlessJava {
    dependsOn(tasks.compileGroovy, tasks.pluginDescriptors, tasks.processResources, tasks.spotlessGroovyGradle, 'spotlessGroovy')
}

tasks.spotlessKotlin {
    dependsOn(tasks.spotlessJava)
}

spotlessCheck.dependsOn('downloadJavaLicense', 'downloadFormatterProperties')
